plugins {
	id 'java'
}

description = 'Structurizr CLI'
group = 'com.structurizr'
version = '1.19.0'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'commons-cli:commons-cli:1.4'

	implementation 'com.structurizr:structurizr-client:1.12.2'

	implementation 'com.structurizr:structurizr-dsl:1.19.1'

	implementation 'com.structurizr:structurizr-documentation:1.0.1'

	implementation 'com.structurizr:structurizr-export:1.5.0'

	implementation 'com.structurizr:structurizr-graphviz:1.6.1'

    implementation 'org.codehaus.groovy:groovy-jsr223:3.0.8'
    implementation 'org.jetbrains.kotlin:kotlin-scripting-jsr223:1.5.30'
    implementation 'org.jruby:jruby-core:9.2.19.0'

	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

test {
	useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Implementation-Title': description,
                'Implementation-Version': version
        )
    }
}

task getDeps(type: Copy) {
	from configurations.default
	into 'build/dependencies'
}

task buildZip(type: Zip) {
	from ('build/libs/') {
		include 'structurizr-cli*.jar'
		into 'lib'
	}
	from ('build/dependencies') {
		include '*.jar'
		into 'lib'
	}
	from ('.') {
		include 'README.md'
		include 'docs/*'
		include 'docs/images/*'
	}
	from ('etc') {
		include 'structurizr.sh'
		include 'structurizr.bat'
	}
}